import java.text.DateFormat
import java.text.SimpleDateFormat

apply from: "$rootDir/build-systems/dependencies.gradle"

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'dagger.hilt.android.plugin'

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("secret.properties")))

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("dd-MM-yyyy")

    return df.format(new Date())
}

android {
    signingConfigs {
        release {
            keyAlias localProperties['keyAlias']
            keyPassword localProperties['keyPassword']
            storeFile file(localProperties['storeFile'])
            storePassword localProperties['storePassword']
        }
        debug {}
    }

    compileSdkVersion appConfig.sdkTarget
    buildToolsVersion appConfig.buildToolsV

    defaultConfig {
        applicationId appConfig.appId
        minSdkVersion appConfig.sdkMin
        targetSdkVersion appConfig.sdkTarget
        versionCode appConfig.buildNumber
        versionName appConfig.nameVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        archivesBaseName = "${applicationId}-v${versionName}#${versionCode}-(${getDateTime()})"
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions "env"
    productFlavors {
        dev {
            dimension "env"

            buildConfigField "String", "GITHUB_AUTH_TOKEN", localProperties['github_auth_token']
            buildConfigField "String", "BASE_URL", localProperties['base_api_dev_url']
            buildConfigField "String", "BASE_IMAGE_MOVIE", localProperties['base_image_url']
            buildConfigField "String", "MOVIE_API_KEY", localProperties['movie_api_key']

            resValue "string", "app_name", "Pipileman - Dev"

            applicationIdSuffix ".dev"
        }
        stag {
            dimension "env"

            buildConfigField "String", "GITHUB_AUTH_TOKEN", localProperties['github_auth_token']
            buildConfigField "String", "BASE_URL", localProperties['base_api_stag_url']
            buildConfigField "String", "BASE_IMAGE_MOVIE", localProperties['base_image_url']
            buildConfigField "String", "MOVIE_API_KEY", localProperties['movie_api_key']

            resValue "string", "app_name", "Pipileman - Stag"

            applicationIdSuffix ".stag"
        }
        prod {
            dimension "env"

            buildConfigField "String", "GITHUB_AUTH_TOKEN", localProperties['github_auth_token']
            buildConfigField "String", "BASE_URL", localProperties['base_api_prod_url']
            buildConfigField "String", "BASE_IMAGE_MOVIE", localProperties['base_image_url']
            buildConfigField "String", "MOVIE_API_KEY", localProperties['movie_api_key']

            resValue "string", "app_name", "Pipileman"

            applicationIdSuffix ""
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation kotlinLibrary.languagePluginX,
            kotlinLibrary.extCoreX

    implementation androidSupportLibrary.activityExt,
            androidSupportLibrary.appCompatX,
            androidSupportLibrary.recyclerX,
            androidSupportLibrary.swipeX,
            androidSupportLibrary.designX,
            androidSupportLibrary.constraintX,
            androidSupportLibrary.roomRuntime,
            androidSupportLibrary.roomKtx,
            androidSupportLibrary.legacySupportV4X
    kapt androidSupportLibrary.roomCompiler

    implementation diLibrary.dagger,
            diLibrary.daggerHiltAndroid,
            diLibrary.daggerHiltAndroidTest,
            diLibrary.hiltCommon,
            diLibrary.hiltViewModel
    kapt diLibrary.daggerCompiler,
            diLibrary.daggerHiltAndroidCompilerKapt,
            diLibrary.hiltCompiler
    androidTestImplementation diLibrary.daggerHiltAndroidTestImpl
    kaptAndroidTest diLibrary.daggerHiltAndroidCompilerKapt

    implementation aacLibrary.lifecycleJava8,
            aacLibrary.lifecycleVmExt

    implementation kotlinLibrary.coroutinesCoreX,
            kotlinLibrary.coroutinesAndroidX,
            kotlinLibrary.serializableX,
            kotlinLibrary.kotlinAdapterSerialConverterX

    implementation squareLibrary.okHttpIntcpt,
            squareLibrary.retrofit,
            squareLibrary.moshiConverter,
            squareLibrary.moshi
    kapt squareLibrary.moshiCodegen

    implementation imageLoader.glide
    kapt imageLoader.glidCompiler

    implementation debugging.stethoDebug

    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'

    testImplementation unitTest.containerTest

    // androidTestImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "com.google.truth:truth:1.0.1"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-rc02'
    androidTestImplementation "com.android.support.test:runner:1.3.0-beta01"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.28.3-alpha'

    // for JVM:
    testImplementation 'org.amshove.kluent:kluent:1.63'

    // for Android:
    testImplementation 'org.amshove.kluent:kluent-android:1.63'

    // To get JUnit errors from kotlin.test, to e.g. enable diff windows in failure messages
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    implementation project(':extensions')
}